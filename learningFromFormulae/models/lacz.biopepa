// location Cell : type=compartment;


Initial_PLac = 1;
Initial_RNAP = 35;   // in Kierzek2002:  ~ N(35, 3.5^2)
Initial_PLacRNAP = 0;
Initial_TrLacZ1 = 0;
Initial_RbsLacZ = 0;
Initial_TrLacZ2 = 0;
Initial_Ribosome = 350;  // in Kierzek2002:  ~ N(350, 35^2)
Initial_RbsRibosome = 0;
Initial_TrRbsLacZ = 0;
Initial_LacZ = 0;
Initial_dgrLacZ = 0;
Initial_dgrRbsLacZ = 0;



// observation in Kierzek2002: stochastic variation is greater for a weak promoter
// two ways to decrease promoter strength:
// - increase RNA polmerase (RNAP) disociation rate (r2 reaction)
//   --> the protein is expressed in bursts
// - increase Ribosome disociation rate (r7 reaction)



// rates as expressed in sec^{-1}
k1 = 0.17;
k2 = 10;
k3 = 1;
k4 = 1;
k5 = 0.015;
k6 = 0.17;
k7 = 0.45;
k8 = 0.4;
k9 = 0.015;
k10 = 6.42e-5;
k11 = 0.3;

kineticLawOf r1 : k1 * (PLac * RNAP) ; // RNA polymarase binding
kineticLawOf r2 : k2 * PLacRNAP ;      // RNA polymarase dissociation

kineticLawOf r3 : k3 * PLacRNAP ;      // closed complex isomerization
kineticLawOf r4 : k4 * TrLacZ1 ;       // promoter clearance

kineticLawOf r5 : k5 * TrLacZ2 ;       // mRNA chain elongation and RNAP release

kineticLawOf r6 : k6 * (Ribosome * RbsLacZ) ; // Ribosome binding
kineticLawOf r7 : k7 * RbsRibosome ;          // Ribosome dissociation
kineticLawOf r8 : k8 * RbsRibosome ;   // RNS clearance

kineticLawOf r9 : k9 * TrRbsLacZ ; // LacZ protein synthesis


// degradation
kineticLawOf r10 : k10 * LacZ ;
kineticLawOf r11 : k11 * RbsLacZ ;



// Promoter (of Lac)
PLac = r1 <<  + r2 >>  + r4 >> ;

// RNA Polymerase
RNAP = r1 <<  + r2 >>  + r5 >> ;


// closed RNAP-propoter complex
PLacRNAP = r1 >>  + r2 <<  + r3 << ;
// open RNAP-propoter complex
TrLacZ1 = r3 >>  + r4 << ;

// RBS --- will bind with ribosome
RbsLacZ = r4 >>  + r6 <<  + r7 >>  + r8 >>  + r11 << ;

// RNA polymerase elongating LacZ mRNA  ---  will release RNAP
TrLacZ2 = r4 >>  + r5 << ;

Ribosome = r6 <<  + r7 >> ;
// RBS-Ribosome complex
RbsRibosome = r6 >>  + r7 <<  + r8 << ;

// ribosome elongating LacZ protein chain
TrRbsLacZ = r8 >>  + r9 << ;
LacZ = r9 >>  + r10 << ;


dgrLacZ = r10 >> ;
dgrRbsLacZ = r11 >> ;



PLac [Initial_PLac] <*>
RNAP [Initial_RNAP] <*>
PLacRNAP [Initial_PLacRNAP] <*>
TrLacZ1 [Initial_TrLacZ1] <*>
RbsLacZ [Initial_RbsLacZ] <*>
TrLacZ2 [Initial_TrLacZ2] <*>
Ribosome [Initial_Ribosome] <*>
RbsRibosome [Initial_RbsRibosome] <*>
TrRbsLacZ [Initial_TrRbsLacZ] <*>
LacZ [Initial_LacZ] <*>
dgrLacZ [Initial_dgrLacZ] <*>
dgrRbsLacZ [Initial_dgrRbsLacZ]
